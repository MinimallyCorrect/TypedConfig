plugins {
	id 'java'
	id 'maven'
}

group = 'org.minimallycorrect.typedconfig'
version = '0.1-SNAPSHOT'

// Extended project information
ext.projectName = 'TypedConfig'
ext.inceptionYear = '2017'
ext.packaging = 'jar'
ext.url = 'https://github.com/MinimallyCorrect/TypedConfig'
ext.description = 'Strongly typed configuration files'
ext.organization = 'minimallycorrect.org'

// ci information
ext.buildNumber = System.getenv("BUILD_NUMBER") ?: 0
ext.ciSystem = System.getenv("JENKINS_URL") ? 'Jenkins' : 'unknown'
ext.commit = System.getenv("GIT_COMMIT") ?: 'unknown'

repositories {
	jcenter()
}

dependencies {
	testCompile 'junit:junit:4.12'
	compileOnly "org.projectlombok:lombok:1.16.16"
	testCompileOnly "org.projectlombok:lombok:1.16.16"
	compileOnly "com.google.code.findbugs:jsr305:3.0.2"
}

tasks.withType(JavaCompile) {
	sourceCompatibility = 8
	targetCompatibility = 8
	options.with {
		encoding = 'UTF-8'
		compilerArgs << "-Xlint:all" << "-Xlint:-classfile" << "-Xlint:-processing"
	}
}

tasks.withType(Jar) {
	baseName = project.name
	manifest.mainAttributes(
		"Built-By": System.properties['user.name'],
		"Created-By": System.properties['java.vm.version'] + " (" + System.properties['java.vm.vendor'] + ")",
		"Implementation-Title": name,
		"Implementation-Version": version + "+" + ciSystem + "-b" + buildNumber + ".git-" + commit,
		"Implementation-Vendor": url
	)
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.5'
}
